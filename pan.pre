# 1 "problem2.pml"
# 1 "<built-in>"
# 1 "<command-line>"
# 1 "problem2.pml"
chan cmConnectRequest = [4] of {int};

mtype:connectReply = {accept, refuse};
chan cmConnectReply[4] = [1] of {mtype:connectReply};

mtype:command = {getInfo, useNewInfo, useOldInfo, disconnect, nothing};
chan cmCommand[4] = [1] of {mtype:command};

mtype:report = {success, failure};
chan clientReport = [4] of {mtype:report, int};

mtype:update = {manualUpdate};
chan wcpRequestCm = [1] of {mtype:update};
chan wcpRequestClient[4] = [1] of {mtype:update};

mtype:able = {enable, disable};
chan cmAbleWcp = [1] of {mtype:able};

mtype:status = {idle,
                preInit, initializing, postInit,
                preUpdate, updating, postUpdate, postRevert};
chan cmStatus[4] = [1] of {mtype:status};

proctype Client(int id) {
    mtype:status currStatus = idle;
    mtype:connectReply reply;
    mtype:command currCommand;
    mtype:update button;
    bool connected = true;
    bool getInfoSuccess = true;
    bool useNewInfoSuccess = true;
    bool useOldInfoSuccess = true;
    do

    :: (!connected && currStatus == idle) ->
        cmConnectRequest!id;
        printf("Client %d: requesting connection\n", id+1);
        cmConnectReply[id]?reply ->
        if
        :: reply == accept -> cmStatus[id]?currStatus; connected = true;
            printf("Client %d: connection request accepted\n", id+1);
        :: else -> skip;
            printf("Client %d: connection request rejected\n", id+1);
        fi
    :: currStatus == preInit ->
        printf("Client %d: pre-initializing\n", id+1);
        cmCommand[id]?currCommand;
        if
        :: currCommand == getInfo -> cmStatus[id]?currStatus;
            printf("Client %d: getting new info\n", id+1);
        :: else -> skip
        fi
    :: currStatus == initializing ->
        printf("Client %d: initializing\n", id+1);
        if
        :: getInfoSuccess -> clientReport!success, id;
            cmCommand[id]?currCommand;
            if
            :: currCommand == useNewInfo -> cmStatus[id]?currStatus;
                printf("Client %d: using new info\n", id+1);
            :: else -> skip
            fi
        :: else -> clientReport!failure, id;
            cmStatus[id]?currStatus; connected = false;
            printf("Client %d: initialization failed. disconnected\n", id+1);
        fi
    :: currStatus == postInit ->
        printf("Client %d: post-initializing\n", id+1);
        if
        :: useNewInfoSuccess -> clientReport!success, id;
            cmStatus[id]?currStatus;
        :: else -> clientReport!failure, id;
            connected = false; cmStatus[id]?currStatus;
            printf("Client %d: post-initialization failed. disconnected\n", id+1);
        fi

    :: (nempty(wcpRequestClient[id]) && currStatus == idle && connected) ->
        wcpRequestClient[id]?button;
        printf("Client %d: manual update request received\n", id+1);
        cmStatus[id]?currStatus;
    :: (currStatus == preUpdate && connected) ->
        printf("Client %d: pre-updating\n", id+1);
        cmCommand[id]?currCommand;
        if
        :: currCommand == getInfo -> cmStatus[id]?currStatus;
            printf("Client %d: getting new info\n", id+1);
        :: else -> skip
        fi
    :: (currStatus == updating && connected) ->
        printf("Client %d: updating\n", id+1);
        if
        :: getInfoSuccess -> clientReport!success, id;
            cmCommand[id]?currCommand;
            if
            :: currCommand == useNewInfo -> cmStatus[id]?currStatus;
                printf("Client %d: using new info\n", id+1);
            :: else -> skip
            fi
        :: else -> clientReport!failure, id;
            cmCommand[id]?currCommand;
            if
            :: currCommand == useOldInfo -> cmStatus[id]?currStatus;
                printf("Client %d: using old info\n", id+1);
            :: else -> skip
            fi
        fi
    :: (currStatus == postUpdate && connected) ->
        printf("Client %d: post-updating\n", id+1);
        if
        :: useNewInfoSuccess -> clientReport!success, id;
            cmStatus[id]?currStatus;
        :: else -> clientReport!failure, id;
            connected = false; cmStatus[id]?currStatus;
        fi
        cmCommand[id]?currCommand;
        if
        :: currCommand == disconnect -> connected = false;
            printf("Client %d: update failed. disconnected\n", id+1);
        :: else -> skip
        fi
    :: (currStatus == postRevert && connected) ->
        printf("Client %d: post-reverting\n", id+1);
        if
        :: useOldInfoSuccess -> clientReport!success, id;
            cmStatus[id]?currStatus;
        :: else -> clientReport!failure, id;
            connected = false; cmStatus[id]?currStatus;
        fi
        cmCommand[id]?currCommand;
        if
        :: currCommand == disconnect -> connected = false;
            printf("Client %d: revert failed. disconnected\n", id+1);
        :: else -> skip
        fi
    od
}

proctype CommsManager() {
    bool connectedClients[4];
    int id;
    int i;
    bool hasFail;
    for (i:0 .. 4-1) {
        connectedClients[i] = true;
    }
    mtype:status currStatus = idle;
    mtype:report reportStatus;
    mtype:update button;
    do

    :: (nempty(cmConnectRequest) && currStatus == idle) ->
        cmConnectRequest?id;
        printf("CommsManager: connection request received from client %d\n", id+1);
        mtype:connectReply reply;
        if
        :: currStatus == idle -> reply = accept;
            currStatus = preInit; cmStatus[id]!preInit;
            connectedClients[id] = true; cmAbleWcp!disable;
        :: else -> reply = refuse;
        fi
        cmConnectReply[id]!reply;
    :: currStatus == preInit ->
        printf("CommsManager: pre-initializing\n");
        cmCommand[id]!getInfo;
        currStatus = initializing; cmStatus[id]!initializing;
    :: currStatus == initializing ->
        printf("CommsManager: initializing\n");
        clientReport?reportStatus, id;
        if
        :: reportStatus == success -> cmCommand[id]!useNewInfo;
            currStatus = postInit; cmStatus[id]!postInit;
        :: reportStatus == failure -> connectedClients[id] = false;
            currStatus = idle; cmStatus[id]!idle;
        fi
    :: currStatus == postInit ->
        printf("CommsManager: post-initializing\n");
        clientReport?reportStatus, id;
        if
        :: reportStatus == success -> currStatus = idle; cmStatus[id]!idle;
            cmAbleWcp!enable;
        :: reportStatus == failure -> connectedClients[id] = false;
            currStatus = idle; cmStatus[id]!idle;
            cmAbleWcp!enable;
        fi

    :: (nempty(wcpRequestCm) && currStatus == idle) ->
        wcpRequestCm?button;
        printf("CommsManager: manual update request received\n");
        for (i:0 .. 4-1){
            if
            :: connectedClients[i] -> wcpRequestClient[i]!button; cmStatus[i]!preUpdate;
            :: else-> skip;
            fi
  }
        currStatus = preUpdate;
        cmAbleWcp!disable;
    :: currStatus == preUpdate ->
        printf("CommsManager: pre-updating\n");
        for (i:0 .. 4-1){
            if
            :: connectedClients[i] -> cmCommand[i]!getInfo; cmStatus[i]!updating;
            :: else-> skip;
            fi
  }
        currStatus = updating;
    :: currStatus == updating ->
        printf("CommsManager: updating\n");
        hasFail = false;
        for (i:0 .. 4-1) {
            if
            :: connectedClients[i] -> clientReport?reportStatus, id;
                if
                :: reportStatus == failure -> hasFail = true;
                    printf("CommsManager: update failed by Client %d\n", i+1);
                :: else -> skip;
                fi
            :: else-> skip;
            fi
  }
        if
        :: hasFail ->
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmCommand[i]!useOldInfo; cmStatus[i]!postRevert;
                :: else-> skip;
                fi
            }
            currStatus = postRevert;
        :: else ->
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmCommand[i]!useNewInfo; cmStatus[i]!postUpdate;
                :: else-> skip;
                fi
            }
            currStatus = postUpdate;
        fi
    :: currStatus == postUpdate ->
        printf("CommsManager: post-updating\n");
        hasFail = false;
        for (i:0 .. 4-1){
            if
            :: connectedClients[i] -> clientReport?reportStatus, id;
                if
                :: reportStatus == failure -> hasFail = true;
                    printf("CommsManager: post-update failed by Client %d\n", i+1);
                :: else -> skip;
                fi
            :: else-> skip;
            fi
  }
        if
        :: hasFail -> currStatus = idle;
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmStatus[i]!idle;
                    connectedClients[i] = false; cmCommand[i]!disconnect;
                :: else -> skip;
                fi
            }
            cmAbleWcp!enable;
        :: else -> currStatus = idle;
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmStatus[i]!idle; cmCommand[i]!nothing;
                :: else -> skip;
                fi
            }
            cmAbleWcp!enable;
        fi
    :: currStatus == postRevert ->
        printf("CommsManager: post-reverting\n");
        hasFail = false;
        for (i:0 .. 4-1){
            if
            :: connectedClients[i] -> clientReport?reportStatus, id;
                if
                :: reportStatus == failure -> hasFail = true;
                    printf("CommsManager: post-revert failed by Client %d\n", i+1);
                :: else -> skip;
                fi
            :: else -> skip;
            fi
  }
        if
        :: hasFail -> currStatus = idle;
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmStatus[i]!idle;
                    connectedClients[i] = false; cmCommand[i]!disconnect;
                :: else -> skip;
                fi
            }
            cmAbleWcp!enable;
        :: else -> currStatus = idle;
            for (i:0 .. 4-1){
                if
                :: connectedClients[i] -> cmStatus[i]!idle; cmCommand[i]!nothing;
                :: else -> skip;
                fi
            }
            cmAbleWcp!enable;
        fi
    od
}

proctype ControlPanel() {
    bool disabled = false;
    mtype:able ability;
    mtype:update button;
    bool isUpdating = false;
    do
    :: nempty(cmAbleWcp) -> cmAbleWcp?ability;
        if
        :: ability == enable -> disabled = false; isUpdating = false;
            printf("ControlPanel: enabled\n");
        :: else -> disabled = true;
            printf("ControlPanel: disabled\n");
        fi
    :: (!disabled && !isUpdating && empty(wcpRequestCm)) ->
        printf("ControlPanel: beginning manual update\n");
        isUpdating = true;
        button = manualUpdate;
        wcpRequestCm!button;
    od
}

init{
 atomic{
  run Client(0);
  run Client(1);
  run Client(2);
  run Client(3);
  run CommsManager();
        run ControlPanel();
 }
}
